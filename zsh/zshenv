# -*- mode: sh; coding: utf-8 -*-

### zsh起動時に設定ファイルが読まれる順番
## 1: /etc/zshenv
## 2: ~/.zshenv
## 3: /etc/zprofile  もしログインシェルなら
## 4: ~/.zprofile    もしログインシェルなら
## 5: /etc/zshrc     もし対話的シェルなら
## 6: ~/.zshrc       もし対話的シェルなら
## 7: /etc/zlogin    もしログインシェルなら
## 8: ~/.zlogin      もしログインシェルなら

### ログアウト時
## 1: ~/.zlogout
## 2: /etc/zlogout



### ファイルが存在すれば読み込む
function source_if_exist {
  [[ -f $1 ]] && source "$@"
}


##################################################
### locale の設定
if [ -z "$LANG" ]; then
  export LANG=ja_JP.UTF-8
fi



##################################################
### パスの設定
## 重複したパスを登録しない。
typeset -U path

## (N-/): 存在しないディレクトリは登録しない。
##    パス(...): ...という条件にマッチするパスのみ残す。
##            N: NULL_GLOBオプションを設定。
##               globがマッチしなかったり存在しないパスを無視する。
##            -: シンボリックリンク先のパスを評価。
##            /: ディレクトリのみ残す。

if type /opt/homebrew/bin/brew > /dev/null 2>&1; then
  eval $(/opt/homebrew/bin/brew shellenv)
elif type /usr/local/bin/brew > /dev/null 2>&1; then
  eval $(/usr/local/bin/brew shellenv)
fi

for formula in grep utils coreutils findutils gnu-tar
do
  path=(${HOMEBREW_PREFIX}/opt/${formula}/libexec/gnubin(N-/) $path)
done

path=(
  ~/bin(N-/)
  ~/local/bin(N-/)
  ~/.nodebrew/current/bin(N-/)
  /usr/local/bin(N-/)
  $path
)

fpath=(
  $ZDOTDIR/functions(N-/)
  ~/g/zsh-completions/src(N-/)
  ${HOMEBREW_PREFIX}/share/zsh/site-functions(N-/)
  $fpath
)


## LD_LIBRARY_PATH
unset LD_LIBRARY_PATH
typeset -xT LD_LIBRARY_PATH ld_library_path
typeset -U ld_library_path
ld_library_path=(
  /opt/local/lib(N-/)
  /usr/local/lib(N-/)
)

## LIBRARY_PATH
unset LIBRARY_PATH
typeset -xT LIBRARY_PATH library_path
typeset -U library_path
library_path=(
  /opt/local/lib(N-/)
  /usr/local/lib(N-/)
)

if type brew > /dev/null 2>&1
then
  local READLINE_PATH="${HOMEBREW_PREFIX}/opt/readline"
  local OPENSSL_PATH="${HOMEBREW_PREFIX}/opt/openssl@1.1"
  export LDFLAGS="-L$READLINE_PATH/lib -L$OPENSSL_PATH/lib"
  export CPPFLAGS="-I$READLINE_PATH/include -I$OPENSSL_PATH/include"
  export PKG_CONFIG_PATH="$READLINE_PATH/lib/pkgconfig:$OPENSSL_PATH/lib/pkgconfig"
  export RUBY_CONFIGURE_OPTS="--with-openssl-dir=$OPENSSL_PATH --with-readline-dir=$READLINE_PATH --disable-install-doc"
  export PATH=$OPENSSL_PATH/bin:$PATH
fi

## XDG
export XDG_CONFIG_HOME="${HOME}/.config"
export XDG_CACHE_HOME="${HOME}/.cache"
export XDG_DATA_HOME="${HOME}/.local/share"
export XDG_STATE_HOME="${HOME}/.local/state"


### コマンドの設定
### PAGER は less にする
export PAGER=less


### less のデフォルトオプション
export LESS='--max-back-scroll=1000 --ignore-case --LONG-PROMPT --RAW-CONTROL-CHARS'


### lv
if type lv > /dev/null 2>&1
then
  ## lv のデフォルトオプション
  ## -c: ANSIエスケープシーケンスの色付けなどを有効にする。
  ## -l: 1行が長くと折り返されていても1行として扱う。
  ##     （コピーしたときに余計な改行を入れない。）
  export LV="-c -l"
fi


### rbenv
if type rbenv > /dev/null 2>&1; then
  eval "$(rbenv init - zsh)"
fi


### エディタの設定 ###
export EDITOR=vim
## vimがなくてもvimでviを起動する。
if ! type vim > /dev/null 2>&1
then
  alias vim=vi
fi



### Emacsのshell modeで動くように
[[ $EMACS = t ]] && unsetopt zle


### メールチェックしない
MAILCHECK=0


### ホストごとの設定を読む
source_if_exist "${ZDOTDIR}/zshenv-${HOST}"
source_if_exist "${ZDOTDIR}/zshenv.local"


### /etc/zprofile等は読まない
setopt no_global_rcs
